plugins {
    id 'java'
}

group = 'kendo.me'
version = '1.0'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } // Spigot API snapshots
    maven { url 'https://repo.skriptlang.org/releases' }
}

// DependÃªncias
dependencies {
    // Spigot API 1.16.5 (apenas para compilar)
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'com.github.SkriptLang:Skript:2.6.4'
    compileOnly files('libs/ProtocolLib.jar') // permite usar as classes no IDE e compilar
    implementation files('libs/ProtocolLib.jar') // IDE autocompletion
}
// Caminho do servidor externo
def serverPluginsDir = "/home/kendo/minecraft-spigot/plugins"

// Task para copiar o JAR do plugin pro servidor
tasks.register("deployPlugin", Copy) {
    description = "Copia o JAR do plugin para a pasta plugins do servidor"
    group = "minecraft"

    dependsOn(tasks.build) // garante que o plugin seja compilado
    from("$buildDir/libs/${rootProject.name}-${version}.jar")
    into(serverPluginsDir)
}

// Copiar automaticamente ao build
tasks.build.finalizedBy(tasks.named("deployPlugin"))

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
